import com.android.build.gradle.LibraryPlugin
import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.aspectj:aspectjtools:1.8.10'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
  }
  buildToolsVersion '25.0.2'
}

dependencies {
  provided "org.aspectj:aspectjrt:1.8.10"

  testCompile deps.junit
  testCompile deps.truth
  testCompile deps.robolectric
  testCompile deps.mockito
}

android.libraryVariants.all { variant ->
  println "tracklytics runtime library variant"
  LibraryPlugin plugin = project.plugins.getPlugin(LibraryPlugin)
  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    println "tracklytics runtime javaCompile.doLast"
    String[] args = [
        "-showWeaveInfo",
        "-1.7",
        "-inpath", javaCompile.destinationDir.toString(),
        "-aspectpath", javaCompile.classpath.asPath,
        "-d", javaCompile.destinationDir.toString(),
        "-classpath", javaCompile.classpath.asPath,
        "-bootclasspath",  project.android.bootClasspath.join(File.pathSeparator)
    ]

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler)

    def log = project.logger
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
        case IMessage.INFO:
          log.info message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug message.message, message.thrown
          break;
      }
    }
  }
}

apply from: rootProject.file('gradle/maven_push.gradle')
